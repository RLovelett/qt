############################################################################
# An install helper for eBinder host filesystem

target_lib|target_app {
    HELPER	=	$$UTILSDIR/eBinder-Install-Helper.sh

    exists($$HELPER) {
        CMD	=	$$HELPER
        target_lib {
            CMD	+=	--dolib
        }
        CMD	+=	--target \""$$TARGET\""
        CMD	+=	--qtlibdir \""$$TOPQTLIBDIR\""
        CMD	+=	--rvctlibdir \""$$RVCT_LIBDIR\""
        shared {
            CMD	+=	--libdir \""$$SHARED_LIBDIR\""
        }
        static {
            CMD	+=	--libdir \""$$STATIC_LIBDIR\""
        }
        CMD	+=	--utilsdir \""$$UTILSDIR\"" '${1+\"$@\"}'
	OUT	=	$$OUT_PWD/hfs-install.sh
        system(rm -f $$OUT)
	system(echo "exec '$$CMD'" >> $$OUT)
	system(chmod 755 $$OUT)

        install-hfs.commands	=	-$$OUT -v -doit
        install-hfs.depends	=	$(TARGET)

        QMAKE_EXTRA_TARGETS	+=	install-hfs
    }
}

